managed implementation in class zbp_03_r_travel unique;
strict ( 2 );
with draft;
define behavior for Z03_R_Travel alias Travel
persistent table z03_travel ##UNMAPPED_FIELD
draft table z03_travel_d
lock master
total etag ChangedAt
authorization master ( instance )
etag master LocChangeAt  //ChangeAt

early numbering
{
  create ( authorization : global );
  update ( features : instance );
  delete;

  action ( features : instance ) cancel_travel;
  draft action Resume;
  draft action Edit;
  draft action Discard;
  draft action Activate optimized;

  validation validateDescription on save { create; update; field Description; }
  validation validateCustomer on save { create; field CustomerId; }
  validation validateBeginDate on save { create; field BeginDate; }
  validation validateEndDate on save { create; field EndDate; }
  validation validateDateSequence on save { create; field BeginDate, EndDate; }
  //  validation validateCustomer2 on save { create; field CustomerId; }
  determination determineDuration on save { field BeginDate, EndDate; }

  determination determineStatus on modify { create; }

  draft determine action Prepare
  {
    validation validateCustomer;
    validation validateDescription;
    validation validateBeginDate;
    validation validateEndDate;
    validation validateDateSequence;
  }

  determine action checkCustomer
  {
    validation validateCustomer;
  }

  determine action adjustDuration
  {
    validation validateBeginDate;
    validation validateEnddate;
    validation validateDateSequence;
    determination determineDuration;
  }

  side effects
  {
    determine action checkCustomer
    executed on field CustomerId
    affects messages;

    determine action adjustDuration
    executed on field BeginDate,
    field EndDate
    affects field Duration, messages;
  }

  //  field ( mandatory : create ) AgencyId, TravelId;
  //  field ( readonly : update ) AgencyId, TravelId;
  field ( readonly ) AgencyId, TravelId;
  field ( readonly ) Status, ChangedAt, ChangedBy, LocChangeAt, Duration;
  field ( mandatory ) Description, EndDate;
  field ( features : instance ) CustomerId, BeginDate;

  //Mapping
  mapping for z03_travel corresponding
    {
      AgencyId    = agency_id;
      TravelId    = travel_id;
      CustomerId  = customer_id;
      BeginDate   = begin_date;
      EndDate     = end_date;
      ChangedAt   = changed_at;
      ChangedBy   = changed_by;
      LocChangeAt = loc_change_at;
    }
}