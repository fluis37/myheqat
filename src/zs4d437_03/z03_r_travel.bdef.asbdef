managed implementation in class zbp_03_r_travel unique;
strict ( 2 );
with draft;

extensible
{
  with additional save;
  with determinations on modify;
  with validations on save;
  with determinations on save;
}

define behavior for Z03_R_Travel alias Travel
persistent table z03_travel ##UNMAPPED_FIELD
draft table z03_travel_d
lock master
total etag ChangedAt
authorization master ( instance )
etag master LocChangeAt  //ChangeAt

early numbering
with additional save
extensible
{
  create ( authorization : global );
  update ( features : instance );
  delete;

  association _TravelItem { create; with draft; }

  action ( features : instance ) cancel_travel;

  event TravelCreated parameter Z03_A_EVENT;

  draft action Resume;
  draft action Edit;
  draft action Discard;
  draft action Activate optimized;

  validation validateDescription on save { create; update; field Description; }
  validation validateCustomer on save { create; field CustomerId; }
  validation validateBeginDate on save { create; field BeginDate; }
  validation validateEndDate on save { create; field EndDate; }
  validation validateDateSequence on save { create; field BeginDate, EndDate; }
  //  validation validateCustomer2 on save { create; field CustomerId; }
  determination determineDuration on save { field BeginDate, EndDate; }

  determination determineStatus on modify { create; }

  draft determine action Prepare
  extensible
  {
    validation validateCustomer;
    validation validateDescription;
    validation validateBeginDate;
    validation validateEndDate;
    validation validateDateSequence;
    validation Item~validateFlightDate;
  }

  determine action checkCustomer
  {
    validation validateCustomer;
  }

  determine action adjustDuration
  {
    validation validateBeginDate;
    validation validateEnddate;
    validation validateDateSequence;
    determination determineDuration;
  }

  side effects
  {
    determine action checkCustomer
    executed on field CustomerId
    affects messages;

    determine action adjustDuration
    executed on field BeginDate,
    field EndDate
    affects field Duration, messages;
  }

  //  field ( mandatory : create ) AgencyId, TravelId;
  //  field ( readonly : update ) AgencyId, TravelId;
  field ( readonly ) AgencyId, TravelId;
  field ( readonly ) Status, ChangedAt, ChangedBy, LocChangeAt, Duration;
  field ( mandatory ) Description, EndDate;
  field ( features : instance ) CustomerId, BeginDate;



  //Mapping
  mapping for z03_travel corresponding ##unextensible_mapping
    {
      AgencyId    = agency_id;
      TravelId    = travel_id;
      CustomerId  = customer_id;
      BeginDate   = begin_date;
      EndDate     = end_date;
      ChangedAt   = changed_at;
      ChangedBy   = changed_by;
      LocChangeAt = loc_change_at;
    }
}

define behavior for Z03_R_TRAVELITEM alias Item
//persistent table z03_tritem
with unmanaged save
draft table z03_tritem_d
authorization dependent by _Travel
lock dependent by _Travel
etag master LocChangedAt
extensible
{

  delete;
  update;

  field ( readonly, numbering : managed ) ItemUuid;
  field ( readonly ) AgencyId, TravelId;

  association _Travel { with draft; }

  validation validateFlightDate on save { create; field FlightDate; }
  determination determineTravelDates on save { field FlightDate; }

  mapping for z03_tritem corresponding ##unextensible_mapping
    {
      ItemUuid           = item_uuid;
      AgencyId           = agency_id;
      TravelId           = travel_id;
      CarrierId          = carrier_id;
      ConnectionId       = connection_id;
      FlightDate         = flight_date;
      BookingId          = booking_id;
      PassengerFirstName = passenger_first_name;
      PassengerLastName  = passenger_last_name;
      ChangedAt          = changed_at;
      ChangedBy          = changed_by;
      LocChangedAt       = loc_changed_at;
    }

  mapping for /lrn/437_s_tritem control /lrn/437_s_tritemx corresponding ##unextensible_mapping
    {
      ItemUuid           = item_uuid;
      AgencyId           = agency_id;
      TravelId           = travel_id;
      CarrierId          = carrier_id;
      ConnectionId       = connection_id;
      FlightDate         = flight_date;
      BookingId          = booking_id;
      PassengerFirstName = passenger_first_name;
      PassengerLastName  = passenger_last_name;
      ChangedAt          = changed_at;
      ChangedBy          = changed_by;
      LocChangedAt       = loc_changed_at;
    }

}