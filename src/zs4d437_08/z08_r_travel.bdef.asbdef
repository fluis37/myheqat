managed implementation in class zbp_08_r_travel unique;
strict ( 2 );

with draft;

define behavior for Z08_R_TRAVEL alias Travel
persistent table z08_travel ##UNMAPPED_FIELD  // unmapped filed pour la zone Duration
draft table z08_travel_d   //mode draft
lock master
total etag ChangedAt  //mode draft

authorization master ( instance )

//Manage data concurrency
//etag master ChangedAt

//mode draft
etag master LocChangedAt

//Numérotation à la sauvegarde via la METHOD earlynumbering_create
//et le Feature control: field ( readonly ) AgencyId, TravelId;
early numbering



{
  //Standard actions
  create ( authorization : global );
  //update;
  update ( features : instance );
  delete;

  //Feature Control
  //field ( readonly : update, mandatory : create ) AgencyId, TravelId;
  field ( readonly ) AgencyId, TravelId;
  // field ( mandatory ) Description, CustomerId, BeginDate, EndDate;
  // field ( mandatory ) Description, EndDate;

  //Gestion zones grisées dynamique
  field ( features : instance ) CustomerId, BeginDate;
  //en mode draft
  field ( readonly ) Status, Duration, ChangedAt, ChangedBy, LocChangedAt;

  // Button
  //action cancel_travel;
  action ( features : instance ) cancel_travel;

  //mode draft
  draft action Edit;
  draft action Activate optimized;  //ne passe pas 2x ds les validations
  draft action Discard;
  draft action Resume;
  draft determine action Prepare
  {
    validation validateDescription;
    validation validateCustomer;
    validation validateBeginDate;
    validation validateEndDate;
    validation validateDateSequence;
  }

  //Validation
  validation validateDescription on save
  {
    create;
    field Description;
  }
  validation validateCustomer on save { create; field CustomerId; }

  validation validateBeginDate on save { create; field BeginDate; }
  validation validateEndDate on save { create; field EndDate; }
  validation validateDateSequence on save { create; field BeginDate, EndDate; }

  //si quelque chose change
  determination determineStatus on modify { create; }

  //
  determine action checkCustomer { validation validateCustomer; }

  //Calcul la durée
  determination determineDuration on save { field BeginDate, EndDate; }
  determine action adjustDuration
  {
    validation validateBeginDate;
    validation validateEnddate;
    validation validateDateSequence;
    determination determineDuration; }
  side effects
  {
    determine action adjustDuration
    executed on field BeginDate,
    field EndDate affects field Duration,
        messages; }

  //Mapping
  mapping for z08_travel corresponding
    {
      AgencyID     = agency_id;
      TravelID     = travel_id;
      CustomerID   = customer_id;
      BeginDate    = begin_date;
      EndDate      = end_date;
      ChangedAt    = changed_at;
      ChangedBy    = changed_by;
      LocChangedAt = loc_changed_at;
    }


}