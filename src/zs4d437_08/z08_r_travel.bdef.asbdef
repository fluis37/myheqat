managed implementation in class zbp_08_r_travel unique;
strict ( 2 );

with draft;

extensible {
with additional save;
with validations on save;
with determinations on modify;
with determinations on save;
}

define behavior for Z08_R_TRAVEL alias Travel
persistent table z08_travel ##UNMAPPED_FIELD  // unmapped field pour la zone Duration
draft table z08_travel_d   //mode draft
lock master
total etag ChangedAt  //mode draft

authorization master ( instance )

//Manage data concurrency
//etag master ChangedAt

//mode draft
etag master LocChangedAt

//Extension
extensible

//Numérotation à la sauvegarde via la METHOD earlynumbering_create
//et le Feature control: field ( readonly ) AgencyId, TravelId;
early numbering

// pour gérer l'envoi de l'évènement
with additional save
{
  association _TravelItem { create; with draft; }

  //Standard actions
  create ( authorization : global );
  //update;
  update ( features : instance );
  delete;

  //Feature Control
  //field ( readonly : update, mandatory : create ) AgencyId, TravelId;
  field ( readonly ) AgencyId, TravelId;
  // field ( mandatory ) Description, CustomerId, BeginDate, EndDate;
  // field ( mandatory ) Description, EndDate;

  //Gestion zones grisées dynamique
  field ( features : instance ) CustomerId, BeginDate;
  //en mode draft
  field ( readonly ) Status, Duration, ChangedAt, ChangedBy, LocChangedAt;

  // Button
  //action cancel_travel;
  action ( features : instance ) cancel_travel;

  //Send an Event
  //event TravelCreated; //sans paramtre
  event TravelCreated parameter Z08_A_Event;

  //mode draft
  draft action Edit;
  draft action Activate optimized;  //ne passe pas 2x ds les validations
  draft action Discard;
  draft action Resume;
  //list of validations that are to be performed during the
  //draft determine action Prepare of the root entity
  draft determine action Prepare
  extensible  //ajoute l'extension
  {
    validation validateDescription;
    validation validateCustomer;
    validation validateBeginDate;
    validation validateEndDate;
    validation validateDateSequence;

    validation Item~validateFlightDate;
  }

  //Validation des champs saisis/modifiés
  validation validateDescription on save
  {
    create;
    field Description;
  }
  validation validateCustomer on save { create; field CustomerId; }

  validation validateBeginDate on save { create; field BeginDate; }
  validation validateEndDate on save { create; field EndDate; }
  validation validateDateSequence on save { create; field BeginDate, EndDate; }

  //si quelque chose change
  determination determineStatus on modify { create; }

  //
  determine action checkCustomer { validation validateCustomer; }

  //Calcul la durée
  determination determineDuration on save { field BeginDate, EndDate; }
  determine action adjustDuration
  {
    validation validateBeginDate;
    validation validateEnddate;
    validation validateDateSequence;
    determination determineDuration; }
  side effects
  {
    determine action adjustDuration
    executed on field BeginDate,
    field EndDate affects field Duration,
        messages; }

  //Mapping
  //mapping for z08_travel corresponding
  //pour l'extension on ajoute un pragma pour éviter le warning
  mapping for z08_travel corresponding  ##unextensible_mapping
    {
      AgencyID     = agency_id;
      TravelID     = travel_id;
      CustomerID   = customer_id;
      BeginDate    = begin_date;
      EndDate      = end_date;
      ChangedAt    = changed_at;
      ChangedBy    = changed_by;
      LocChangedAt = loc_changed_at;
    }

}

define behavior for Z08_R_TRAVELITEM alias Item
//persistent table z08_tritem
//with additional save                  //ajout de la partie additional save
with unmanaged save
draft table z08_tritem_d
authorization dependent by _Travel
lock dependent by _Travel
etag master LocChangedAt

//Extension
extensible

{
  association _Travel { with draft; }

  update;
  delete;

  //Feature Control
  field ( readonly, numbering : managed )
  ItemUuid;  //internal numbering for technical key field ITemUuid

  field ( readonly )
  AgencyId,
  TravelId;

  //Validation des champs saisis/modifiés
  validation validateFlightDate on save
  { create;
    field FlightDate;
  }

  determination determineTravelDates on save
  {
    field FlightDate;
  }




  //Mapping
  mapping for z08_tritem corresponding
    {
      ItemUuid           = item_uuid;
      AgencyId           = agency_id;
      TravelId           = travel_id;
      CarrierId          = carrier_id;
      ConnectionId       = connection_id;
      FlightDate         = flight_date;
      BookingId          = booking_id;
      PassengerFirstName = passenger_first_name;
      PassengerLastName  = passenger_last_name;
      ChangedAt          = changed_at;
      ChangedBy          = changed_by;
      LocChangedAt       = loc_changed_at;
    }

  mapping for /lrn/437_s_tritem
    control /lrn/437_s_tritemx
    corresponding
    {
      ItemUuid           = item_uuid;
      AgencyId           = agency_id;
      TravelId           = travel_id;
      CarrierId          = carrier_id;
      ConnectionId       = connection_id;
      FlightDate         = flight_date;
      BookingId          = booking_id;
      PassengerFirstName = passenger_first_name;
      PassengerLastName  = passenger_last_name;
      ChangedAt          = changed_at;
      ChangedBy          = changed_by;
      LocChangedAt       = loc_changed_at;
    }

}