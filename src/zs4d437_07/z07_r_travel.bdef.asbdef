managed implementation in class zbp_07_r_travel unique;
strict ( 2 );
with draft;
extensible
{
with additional save;
with validations on save;
with determinations on modify;
with determinations on save;
}
define behavior for Z07_R_Travel alias Travel
persistent table z07_travel ##UNMAPPED_FIELD
draft table z07_travel_d
//lock with additional savetional saveional saverolhter
lock master
total etag ChangedAt
authorization master ( instance )
//etag master ChangedAt
etag master LocChangedAt
early numbering
with additional save
extensible
{
  create ( authorization : global );
  //  update;
  update ( features : instance );
  delete;
  field ( readonly ) Status, Duration, ChangedAt, ChangedBy, LocChangedAt;
  //  field ( readonly : update, mandatory : create ) AgencyId, TravelId;
  field ( readonly ) AgencyId, TravelId;
  //  field ( mandatory ) Description, CustomerId, BeginDate, EndDate;

  field ( mandatory ) Description,
  // CustomerId,
  // BeginDate,
  EndDate;
  field ( features : instance )
  CustomerId,
  BeginDate;

  association _TravelItem { create; with draft; }

  //  action cancel_travel;
  action ( features : instance ) cancel_travel;

  event TravelCreated parameter Z07_A_Event;

  validation validateDescription on save
  {
    create;
    field Description;
  }

  validation validateCustomer on save
  {
    create;
    field CustomerId;
  }

  validation validateBeginDate on save
  {
    create;
    field BeginDate;
  }
  validation validateEndDate on save
  {
    create;
    field EndDate;
  }
  validation validateDateSequence on save
  {
    create;
    field BeginDate, EndDate;
  }

  determination determineStatus on modify
  { create; }

  determine action checkCustomer
  {
    validation validateCustomer;
  }

  determination determineDuration on save
  {
    field BeginDate,
    EndDate;
  }

  side effects
  {
    determine action checkCustomer
    executed on field CustomerId
    affects messages;
    determine action adjustDuration executed on field BeginDate, field EndDate
    affects field Duration, messages;
  }

  draft action Edit;
  draft action Activate optimized;
  draft action Discard;
  draft action Resume;
  draft determine action Prepare
  extensible
  {
    validation validateDescription;
    validation validateCustomer;
    validation validateBeginDate;
    validation validateEndDate;
    validation validateDateSequence;
    validation Item~validateFlightDate;
  }

  determine action adjustDuration
  {
    validation validateBeginDate;
    validation validateEnddate;
    validation validateDateSequence;
    determination determineDuration;
  }

  mapping for z07_travel corresponding ##unextensible_mapping
    {
      AgencyID     = agency_id;
      TravelID     = travel_id;
      CustomerID   = customer_id;
      BeginDate    = begin_date;
      EndDate      = end_date;
      ChangedAt    = changed_at;
      ChangedBy    = changed_by;
      LocChangedAt = loc_changed_at;
    }
}
define behavior for Z07_R_TRAVELITEM alias Item
//persistent table z07_tritem
//with additional save
with unmanaged save
draft table z07_tritem_d
authorization dependent by _Travel
lock dependent by _Travel
//etag master LocChangedAt
extensible
{
  update;
  delete;

  field ( readonly, numbering : managed )
  ItemUuid;
  field ( readonly )
  AgencyId, TravelId;

  validation validateFlightDate on save
  {
    create;
    field FlightDate;
  }

  determination determineTravelDates on save
  {
    field FlightDate;
  }

  association _Travel { with draft; }

  mapping for z07_tritem corresponding ##unextensible_mapping
    {
      ItemUuid           = item_uuid;
      AgencyId           = agency_id;
      TravelId           = travel_id;
      CarrierId          = carrier_id;
      ConnectionId       = connection_id;
      FlightDate         = flight_date;
      BookingId          = booking_id;
      PassengerFirstName = passenger_first_name;
      PassengerLastName  = passenger_last_name;
      ChangedAt          = changed_at;
      ChangedBy          = changed_by;
      LocChangedAt       = loc_changed_at;
    }

  mapping for /lrn/437_s_tritem
  control /lrn/437_s_tritemx corresponding ##unextensible_mapping
    {
      ItemUuid           = item_uuid;
      AgencyId           = agency_id;
      TravelId           = travel_id;
      CarrierId          = carrier_id;
      ConnectionId       = connection_id;
      FlightDate         = flight_date;
      BookingId          = booking_id;
      PassengerFirstName = passenger_first_name;
      PassengerLastName  = passenger_last_name;
      ChangedAt          = changed_at;
      ChangedBy          = changed_by;
      LocChangedAt       = loc_changed_at;
    }
}