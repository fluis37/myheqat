managed implementation in class zbp_00_r_travel unique;
strict ( 2 );

with draft;

extensible
{
  with additional save;
  with validations on save;
  with determinations on modify;
  with determinations on save;
}



define behavior for Z00_R_Travel alias Travel
persistent table z00_travel
draft table z00_travel_d
lock master
total etag ChangedAt
authorization master ( instance )
etag master LocChangedAt
early numbering
with additional save

extensible
{
  // Standard Operations
  create ( authorization : global );
  update ( features : instance );
  delete;

 event TravelCreated parameter /LRN/437g_A_Event;

  //Feature Control

  field ( readonly ) AgencyId, TravelId;
  field ( readonly ) Status, Duration, ChangedAt, ChangedBy, LocChangedAt;
  field ( mandatory ) Description, EndDate;
  field ( features : instance ) CustomerId, BeginDate;

  //Non-Standard operations

  action issue_message;
  static action static_ation;
  action ( features : instance ) cancel_travel;

  // Validations
  validation validateDescription on save
  { create;
    field Description;
  }

  validation validateCustomer on save
  { create;
    field CustomerId;
  }

  validation validateBeginDate on save
  { create;
    field BeginDate;
  }

  validation validateEndDate on save
  { create;
    field EndDate;
  }

  validation validateDateSequence on save
  { create;
    field BeginDate,
    EndDate;
  }

  //Determination
  determination determineStatus on modify
  { create;
  }
  determine action checkCustomer
  {
    validation validateCustomer;
  }
  determination determineDuration on save
  {
    field BeginDate, EndDate;
  }
  determine action adjustDuration
  {
    validation validateBeginDate;
    validation validateEndDate;
    validation validateDateSequence;
    validation Item~ValidateFlightDate;

    determination determineDuration;
  }



  // Draft
  draft action Edit;
  draft action Activate optimized;

  draft action Discard;
  draft action Resume;
  draft determine action Prepare
  extensible
  {
    validation validateCustomer;
    validation validateDescription;
    validation validateBeginDate;
    validation validateEnddate;
    validation validateDateSequence;

    validation Item~ValidateFlightDate;

  }
  //Side effects
  side effects
  {
    determine action checkCustomer executed on field CustomerId affects messages;

    determine action adjustDuration executed on field BeginDate, field EndDate affects field Duration, messages;
  }
  //Association
  association _TravelItem { create; with draft; }
  //Mapping
  mapping for z00_travel corresponding ##UNEXTENSIBLE_MAPPING
    {
      AgencyId     = agency_id;
      TravelId     = travel_id;
      CustomerId   = customer_id;
      BeginDate    = begin_date;
      EndDate      = end_date;
      ChangedAt    = changed_at;
      ChangedBy    = changed_by;
      LocChangedAt = loc_change_at;
    }
}

define behavior for Z00_R_TravelItem alias Item
//persistent table z00_tritem
draft table z00_trit_d

authorization dependent by _Travel
lock dependent by _Travel
etag master LocChangedAt
with unmanaged save
extensible
{

  update;
  delete;

  association _Travel { with draft; }

  field ( readonly, numbering : managed )
  ItemUuid;

  field ( readonly )
  AgencyId,
  TravelId;

  validation validateFlightDate on save
  { create;
    field FlightDate;
  }

  determination determineTravelDates on save
  {
    field FlightDate;
  }
  mapping for /lrn/437_s_tritem
  control /lrn/437_s_tritemx
  corresponding ##UNEXTENSIBLE_MAPPING
    {
      ItemUuid           = item_uuid;
      AgencyId           = agency_id;
      TravelId           = travel_id;
      CarrierId          = carrier_id;
      ConnectionId       = connection_id;
      FlightDate         = flight_date;
      BookingId          = booking_id;
      PassengerFirstName = passenger_first_name;
      PassengerLastName  = passenger_last_name;
      ChangedAt          = changed_at;
      ChangedBy          = changed_by;
      LocChangedAt       = loc_changed_at;
    }
//  mapping for z00_tritem corresponding
//    {
//      ItemUuid           = item_uuid;
//      AgencyId           = agency_id;
//      TravelId           = travel_id;
//      CarrierId          = carrier_id;
//      ConnectionId       = connection_id;
//      FlightDate         = flight_date;
//      BookingId          = booking_id;
//      PassengerFirstName = passenger_first_name;
//      PassengerLastName  = passenger_last_name;
//      ChangedAt          = changed_at;
//      ChangedBy          = changed_by;
//      LocChangedAt       = loc_changed_at;
//    }
}