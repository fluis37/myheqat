managed implementation in class zbp_02_r_travel unique;
strict ( 2 );

with draft;

extensible
{
    with additional save;
    with validations on save;
    with determinations on modify;
    with determinations on save;
}

define behavior for Z02_R_TRAVEL alias Travel
persistent table z02_travel ##UNMAPPED_FIELD
with additional save
draft table z02_travel_d
lock master
total etag ChangedAt
authorization master ( instance )
etag master LocChangedAt
early numbering
extensible
{
  association _TravelItem { create; with draft; }

  // Standard Operations
  create ( authorization : global );
  update ( features : instance );
  delete;

  // Feature Control
//  field ( readonly: update, mandatory: create )
  field ( readonly )
  AgencyId,
  TravelId;

  field (readonly )
  Status,
  Duration,
  ChangedAt,
  ChangedBy,
  LocChangedAt;

  field (mandatory)
  Description,
//  CustomerId,
//  BeginDate,
  EndDate;

  field ( features : instance )
  CustomerId,
  BeginDate;

  // Custom Operations
  action ( features : instance ) cancel_travel;

  event TravelCreated parameter Z02_A_Event;

  determine action checkCustomer {
    validation validateCustomer;
  }

  determine action adjustDuration {
    validation validateBeginDate;
    validation validateEnddate;
    validation validateDateSequence;

    determination determineDuration;
  }

  side effects {
    determine action adjustDuration
      executed on field BeginDate,
                  field EndDate
      affects field Duration, messages;
  }

  determination determineDuration on save {
    field BeginDate, EndDate;
  }

  draft action Edit;
  draft action Activate optimized;
  draft action Discard;
  draft action Resume;
  draft determine action Prepare extensible {
    validation validateDescription;
    validation validateCustomer;
    validation validateBeginDate;
    validation validateEndDate;
    validation validateDateSequence;

    validation Item~validateFlightDate;
  }

  // mapping
  mapping for z02_travel ##unextensible_mapping {
    AgencyId = agency_id;
    BeginDate = begin_date;
    ChangedAt = changed_at;
    ChangedBy = changed_by;
    CustomerId = customer_id;
    Description = description;
    EndDate = end_date;
    Status = status;
    TravelId = travel_id;
    LocChangedAt = loc_changed_at;
  }

  validation validateDescription on save {
    create;
    field Description;
  }

  validation validateCustomer on save {
    create;
    field CustomerId;
  }

  validation validateBeginDate on save {
    create;
    field BeginDate;
  }

  validation validateEndDate on save {
    create;
    field EndDate;
  }

  validation validateDateSequence on save {
    create;
    field BeginDate, EndDate;
  }

  determination determineStatus on modify {
    create;
  }

}

define behavior for Z02_R_TravelItem alias Item
with unmanaged save
//persistent table z02_tritem
//with additional save
draft table z02_tritem_d
authorization dependent by _Travel
lock dependent by _Travel
etag master LocChangedAt
extensible
{
  update;
  delete;
	
  association _Travel { with draft; }

  field ( readonly, numbering : managed )
  ItemUuid;

  field ( readonly )
  AgencyId,
  TravelId;

  validation validateFlightDate on save {
    create;
    field FlightDate;
  }
  determination determineTravelDate on save {
    field FlightDate;
  }

  mapping for z02_tritem corresponding ##unextensible_mapping
  {
	ItemUuid = item_uuid;
	AgencyId = agency_id;
	TravelId = travel_id;
	CarrierId = carrier_id;
	ConnectionId = connection_id;
	FlightDate = flight_date;
	BookingId = booking_id;
	PassengerFirstName = passenger_first_name;
	PassengerLastName = passenger_last_name;
	ChangedAt = changed_at;
	ChangedBy = changed_by;
	LocChangedAt = loc_changed_at;
  }
  mapping for /lrn/437_s_tritem
    control /lrn/437_s_tritemx corresponding ##unextensible_mapping
  {
    ItemUuid = item_uuid;
    AgencyId = agency_id;
    TravelId = travel_id;
    CarrierId = carrier_id;
    ConnectionId = connection_id;
    FlightDate = flight_date;
    BookingId = booking_id;
    PassengerFirstName = passenger_first_name;
    PassengerLastName = passenger_last_name;
    ChangedAt = changed_at;
    ChangedBy = changed_by;
    LocChangedAt = loc_changed_at;
  }

}