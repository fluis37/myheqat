managed implementation in class zbp_02_r_travel unique;
strict ( 2 );

with draft;

define behavior for Z02_R_TRAVEL alias Travel
persistent table z02_travel ##UNMAPPED_FIELD
draft table z02_travel_d
lock master
total etag ChangedAt
authorization master ( instance )
etag master LocChangedAt
early numbering
{
  // Standard Operations
  create ( authorization : global );
  update ( features : instance );
  delete;

  // Feature Control
//  field ( readonly: update, mandatory: create )
  field ( readonly )
  AgencyId,
  TravelId;

  field (readonly )
  Status,
  Duration,
  ChangedAt,
  ChangedBy,
  LocChangedAt;

  field (mandatory)
  Description,
//  CustomerId,
//  BeginDate,
  EndDate;

  field ( features : instance )
  CustomerId,
  BeginDate;

  // Custom Operations
  action ( features : instance ) cancel_travel;

  determine action checkCustomer {
    validation validateCustomer;
  }

  determine action adjustDuration {
    validation validateBeginDate;
    validation validateEnddate;
    validation validateDateSequence;

    determination determineDuration;
  }

  side effects {
    determine action adjustDuration
      executed on field BeginDate,
                  field EndDate
      affects field Duration, messages;
  }

  determination determineDuration on save {
    field BeginDate, EndDate;
  }

  draft action Edit;
  draft action Activate optimized;
  draft action Discard;
  draft action Resume;
  draft determine action Prepare {
    validation validateDescription;
    validation validateCustomer;
    validation validateBeginDate;
    validation validateEndDate;
    validation validateDateSequence;
  }

  // mapping
  mapping for z02_travel {
    AgencyId = agency_id;
    BeginDate = begin_date;
    ChangedAt = changed_at;
    ChangedBy = changed_by;
    CustomerId = customer_id;
    Description = description;
    EndDate = end_date;
    Status = status;
    TravelId = travel_id;
    LocChangedAt = loc_changed_at;
  }

  validation validateDescription on save {
    create;
    field Description;
  }

  validation validateCustomer on save {
    create;
    field CustomerId;
  }

  validation validateBeginDate on save {
    create;
    field BeginDate;
  }

  validation validateEndDate on save {
    create;
    field EndDate;
  }

  validation validateDateSequence on save {
    create;
    field BeginDate, EndDate;
  }

  determination determineStatus on modify {
    create;
  }

}