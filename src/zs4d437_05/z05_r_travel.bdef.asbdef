managed implementation in class zbp_05_r_travel unique;
strict ( 2 );
with draft;
extensible
{
  with additional save;
  with validations on save;
  with determinations on modify;
  with determinations on save;
}

define behavior for Z05_R_Travel alias Travel
persistent table z05_travel ##UNMAPPED_FIELD
draft table z05_travel_d // draft table
//lock master
lock master total etag ChangedAt
authorization master ( instance )
//etag master ChangedAt
etag master LocChangedAt
early numbering
with additional save
extensible
{
  create ( authorization : global );
  //  update;
  update ( features : instance );
  delete;

  association _TravelItem { create; with draft; }

  //ACTIONS
  //  action cancel_travel;
  action ( features : instance ) cancel_travel;
  event TravelCreated parameter Z05_A_Event;
  draft action Edit;
  draft action Activate optimized;
  draft action Discard;
  draft action Resume;

  draft determine action Prepare
  extensible
  {
    validation validateDescription;
    validation validateCustomer;
    validation validateBeginDate;
    validation validateEndDate;
    validation validateDateSequence;
    validation Item~validateFlightDate; // ???
  }

  //FIELDS

  field ( readonly ) AgencyId, TravelId;

  field ( readonly )
  Status,
  Duration,
  ChangedAt,
  ChangedBy,
  LocChangedAt;

  field ( mandatory )
  Description,

  //  CustomerId,
  //  BeginDate,
  EndDate;

  field ( features : instance )
  CustomerId,
  BeginDate;


  //VALIDATIONS

  validation validateDescription on save
  { create;
    field Description;
  }

  validation validateCustomer on save
  { create;
    field CustomerId;
  }

  validation validateBeginDate on save
  { create;
    field BeginDate;
  }

  validation validateEndDate on save
  { create;
    field EndDate;
  }

  validation validateDateSequence on save
  { create;
    field BeginDate,
    EndDate;
  }


  //DETERMINATIONS
  determination determineStatus on modify
  { create;
  }

  determination determineDuration on save
  { field BeginDate,
    EndDate; }

  //DETERMINE ACTION
  determine action checkCustomer
  {
    validation validateCustomer;
  }


  //MAPPING
  mapping for z05_travel ##unextensible_mapping
    {
      AgencyId     = agency_id;
      TravelId     = travel_id;
      Description  = description;
      CustomerId   = customer_id;
      BeginDate    = begin_date;
      Enddate      = end_date;
      Status       = status;
      ChangedAt    = changed_at;
      ChangedBy    = changed_by;
      LocChangedAt = loc_changed_at;
    }
}

define behavior for Z05_R_TRAVELITEM alias Item
//persistent table z05_tritem
with unmanaged save
draft table z05_tritem_d
authorization dependent by _Travel
lock dependent by _Travel
etag master LocChangedAt
extensible
{
  update;
  delete;

  association _Travel { with draft; }

  field ( readonly, numbering : managed )
  ItemUuid;

  field ( readonly )
  AgencyId,
  TravelId;

  validation validateFlightDate on save
  {
    create;
    field FlightDate;
  }

  determination determineTravelDates on save
  {
    field FlightDate;
  }

  //MAPPINGS
  mapping for z05_tritem corresponding
    {
      ItemUuid           = item_uuid;
      AgencyId           = agency_id;
      TravelId           = travel_id;
      CarrierId          = carrier_id;
      ConnectionId       = connection_id;
      FlightDate         = flight_date;
      BookingId          = booking_id;
      PassengerFirstName = passenger_first_name;
      PassengerLastName  = passenger_last_name;
      ChangedAt          = changed_at;
      ChangedBy          = changed_by;
      LocChangedAt       = loc_changed_at;
    }

  mapping for /lrn/437_s_tritem
  control /lrn/437_s_tritemx
  corresponding
    {
      ItemUuid           = item_uuid;
      AgencyId           = agency_id;
      TravelId           = travel_id;
      CarrierId          = carrier_id;
      ConnectionId       = connection_id;
      FlightDate         = flight_date;
      BookingId          = booking_id;
      PassengerFirstName = passenger_first_name;
      PassengerLastName  = passenger_last_name;
      ChangedAt          = changed_at;
      ChangedBy          = changed_by;
      LocChangedAt       = loc_changed_at;
    }


}